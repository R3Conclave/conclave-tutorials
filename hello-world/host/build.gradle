plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.4'
    id "io.spring.dependency-management" version "1.1.0"
}

// Override the default (mock) with -PenclaveMode=
def mode = findProperty("enclaveMode")?.toString()?.toLowerCase() ?: "mock"

// Create a task that can be used for generating signing materials.
// Only required if you're building signed/release version.
tasks.register("prepareForSigning") {
    it.dependsOn(":enclave:generateEnclaveSigningMaterial${mode.capitalize()}")
}

dependencies {
    // *your* enclave
    runtimeOnly project(path: ":enclave", configuration: mode)
    // Use the host web server for receiving and sending mail to the clients. This means the client needs to use the
    // conclave-web-client library.
    runtimeOnly "com.r3.conclave:conclave-web-host:$conclaveVersion"

    // Change this to your preferred logging framework
    runtimeOnly 'ch.qos.logback:logback-classic'

    // Enable unit tests
    testImplementation "com.r3.conclave:conclave-host:$conclaveVersion"
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

bootJar {
    // conclave-web-host provides us with the main method to use.
    mainClass.set("com.r3.conclave.host.web.EnclaveWebHost")
    // Include the enclave mode in the filename for clarity.
    archiveClassifier.set(mode)
}
